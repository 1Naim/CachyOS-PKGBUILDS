# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-sysctl-manager
pkgver=1.1.1
pkgrel=1
pkgdesc="Manage linux kernel sysctl options"
arch=(aarch64 x86_64)
url="https://github.com/cachyos/sysctl-manager"
license=(GPL-3.0-or-later)
depends=('qt6-base' 'polkit')
makedepends=('cmake' 'ninja' 'git' 'polkit-qt6')
groups=('cachyos')
source=("sysctl-manager-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('8c656ae12735eab8d01127589c80b98e4a31108a501aa2b1c11b4ce29b6fca8c')

build() {
  cd ${srcdir}/sysctl-manager-${pkgver}

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {
  cd ${srcdir}/sysctl-manager-${pkgver}
  DESTDIR="${pkgdir}" cmake --build build --target install
}

# vim:set sw=2 sts=2 et:
