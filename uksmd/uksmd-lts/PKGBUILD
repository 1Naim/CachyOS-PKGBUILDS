# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Oleksandr Natalenko <oleksandr@natalenko.name>
# Contributor: Damian N. <nycko123 at gmail>

pkgname=uksmd-lts
_pkgname=uksmd
pkgver=1.1.0
pkgrel=2
epoch=1
pkgdesc="Userspace KSM helper daemon"
url="https://github.com/CachyOS/uksmd"
license=(GPL-3.0-or-later)
arch=('x86_64')
depends=('libcap-ng' 'libprocps')
makedepends=('meson')
conflicts=('uksmd' 'uksmd-git')
provides=("uksmd=${pkgver}")
source=("$url/archive/v$pkgver.tar.gz"
        '0001-uksmdstats-fixes.patch')
sha256sums=('170976c3ca559f531d650b5b530a49ac24b61d5df8c2d4cf2fdc0bc08f6a74a2'
            '796a3bdc3e5c34a4772c53df415d188707553c31d5734bfb92ed6b8d223d1750')

prepare() {
 cd $_pkgname-$pkgver

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_pkgname-$pkgver
  arch-meson . build
  meson compile -C build
}

package() {
  depends+=(UKSMD-BUILTIN)

  cd $_pkgname-$pkgver
  meson install -C build --destdir "${pkgdir}"
}
