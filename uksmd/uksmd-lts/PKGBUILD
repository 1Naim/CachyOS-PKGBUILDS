# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Oleksandr Natalenko <oleksandr@natalenko.name>
# Contributor: Damian N. <nycko123 at gmail>

pkgname=uksmd-lts
_pkgname=uksmd
pkgver=1.1.0
pkgrel=1
epoch=1
pkgdesc="Userspace KSM helper daemon"
url="https://github.com/CachyOS/uksmd"
license=('GPL3')
arch=('x86_64')
depends=('libcap-ng' 'libprocps')
makedepends=('meson' 'git')
conflicts=('uksmd')
provides=("uksmd=${pkgver}")
source=("$url/archive/v$pkgver.tar.gz")
sha256sums=('170976c3ca559f531d650b5b530a49ac24b61d5df8c2d4cf2fdc0bc08f6a74a2')

prepare() {
 cd $_pkgname-$pkgver

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_pkgname-$pkgver
  arch-meson . build
  meson compile -C build
}

package() {
  depends+=(UKSMD-BUILTIN)

  cd $_pkgname-$pkgver
  meson install -C build --destdir "${pkgdir}"
}
