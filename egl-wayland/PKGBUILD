# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=egl-wayland
pkgver=1.1.15
pkgrel=1
epoch=3
pkgdesc="EGLStream-based Wayland external platform"
url="https://github.com/NVIDIA/egl-wayland"
arch=(x86_64)
license=(MIT)
depends=(
  eglexternalplatform
  glibc
  libdrm
  wayland
)
makedepends=(
  git
  'libglvnd>=1.3.4'
  meson
  wayland-protocols
)
provides=(libnvidia-egl-wayland.so)
source=(
  "git+$url#commit=448034502fdabc4ec60fb94051f981c5a901103b"
  10_nvidia_wayland.json
  new-release.patch::https://github.com/NVIDIA/egl-wayland/commit/5b7fa06c0da810efca181410ebd9c069d4dffd6b.patch
)
b2sums=('72e9c03f40829b344dd2a83b5975d72635479388add238ccc5837a9c81d9bef2daed578bb715c0612f55dcbc0fb135bc01210ee7dddeb40fbe91727b355db0c7'
        'b10206c742e8966d1192b9b0604137e6b296d2be74a437841c63844c0716343578b11565a34fb4c534d5908c0b5775305581b68039a6ff9ed7421c9d385a2b7a'
        'e43e959158eef21b3c9ba31912673e56c2e7220c13f688263ee6ed38b05ddd5d5decaf128eeefccb64f6156eaa4e37003ca4e75d34fcd7468122684b364adb1c')

prepare() {
  cd $pkgname
  patch -Np1 < ../new-release.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/egl/egl_external_platform.d" -m644 10_nvidia_wayland.json
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 sts=-1 et:
