# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=egl-wayland
pkgver=1.1.14
pkgrel=2
epoch=3
pkgdesc="EGLStream-based Wayland external platform"
url="https://github.com/NVIDIA/egl-wayland"
arch=(x86_64)
license=(MIT)
depends=(
  eglexternalplatform
  wayland
)
makedepends=(
  git
  'libglvnd>=1.3.4'
  meson
  wayland-protocols
)
provides=(libnvidia-egl-wayland.so)
_commit=4f4c965c773a346802415155eb49e07ef0161159  # tags/1.1.14
source=(
  "git+$url#commit=$_commit"
  10_nvidia_wayland.json
  0001-egl-wayland.patch
  crashfix.patch::https://github.com/NVIDIA/egl-wayland/commit/a5182c7390a78ca2f7986cbcd2e1bf38f6be5f47.patch ## Fix for crashes in firefox, obs and co
)
b2sums=('0f2a1a7dbdc6428122c15421187cfbbc938355a4c0b74a370af0c76e0dd638841aa055c6c93df72b02826e41f4ddd3cf5a2cbd7b779d12740650d5b31adf6c20'
        'b10206c742e8966d1192b9b0604137e6b296d2be74a437841c63844c0716343578b11565a34fb4c534d5908c0b5775305581b68039a6ff9ed7421c9d385a2b7a'
        '24ff3ef29fd7b3df55d44b48c48d38c4714150d598f75cef7182d1c0b87f89638d5a598eccaad4ddf9fe8028d20f87d8c570f489564b12fe7ac25f91b15cc642'
        'c8473d4e6464c19b597d7c2bb18b709707a89e0947a137d06ad2cf2d25fc0ae7ec005c4702dc8095289042d12c365962f2d9ed875f643c86f622253b6b8b8e52')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 < ../0001-egl-wayland.patch
  patch -Np1 < ../crashfix.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/egl/egl_external_platform.d" -m644 10_nvidia_wayland.json
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 sts=-1 et:
