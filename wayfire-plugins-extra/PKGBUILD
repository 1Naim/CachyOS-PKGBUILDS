# Maintainer: Adrian Perez de Castro <aperez@igalia.com>

pkgname=wayfire-plugins-extra
pkgver=0.8.0
pkgrel=1
pkgdesc='Additional plugins for Wayfire'
url=https://github.com/WayfireWM/wayfire-plugins-extra
arch=(x86_64)
license=(custom:MIT)
conflicts=("${pkgname}-git")
depends=('wayfire>=0.7' cairo glibmm wlroots iio-sensor-proxy)
makedepends=(meson wayland-protocols ninja glm git)
_commit=5076dc1100071d8ed220e06a4723bde16ac864f7
source=("${pkgname}::git+$url.git#commit=${_commit}")
sha512sums=('SKIP')
options=(strip)

build () {
  cd ${srcdir}/$pkgname
  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  arch-meson build
  meson compile -C build --jobs $_cpuCount
}

check () {
  meson test -C build
}

package () {
  cd "${srcdir}/$pkgname/build"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}/$pkgname"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
