# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Gorski <piotrgorski@cachyos.org>

pkgname=cachyos-ananicy-rules
_gitname=ananicy-rules
pkgver=1.r361.167915d
pkgrel=1
groups=(cachyos)
arch=('any')
license=(GPL-1.0-only)
pkgdesc='CachyOS - ananicy-rules'
source=("${pkgname}::git+https://github.com/CachyOS/${_gitname}.git#commit=167915d1c6e4f46932a9857b1a4aeb3e813a8538")
sha256sums=('8f89879ef391597869fdb7078adcbfe5b92c902810d36558c01ac18977081f12')
replaces=(ananicy-rules-git)
provides=(ananicy-rules-git)
conflicts=(ananicy-rules-git)

pkgver() {
  cd "${srcdir}/${pkgname}"
  _gittag="1"
  printf "${_gittag}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
prepare() {
  cd "${pkgname}"
  rm -f README.md
}
package() {
  cd "${pkgname}"
  install -d $pkgdir/etc/ananicy.d
  cp -rf $srcdir/$pkgname/* $pkgdir/etc/ananicy.d
}
