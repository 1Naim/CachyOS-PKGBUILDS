# Maintainer: NextWork123 <nextworks@protonmail.com>
# Maintainer: Vendicated <vendicated at riseup dot net>


pkgname=armcord-git
pkgdesc="ArmCord dev build using the system electron. ArmCord is a custom client designed to enhance your Discord experience while keeping everything lightweight"
pkgver=r978.447300d
pkgrel=1

arch=("x86_64")
url="https://github.com/NextWork123/ArmCord"
license=("custom:OSL-3.0")

makedepends=("git" "nodejs" "npm")
depends=("electron27-bin")  # Use electron26-bin instead of electron
optdepends=(
  'libnotify: Notifications'
  'xdg-utils: Open links, files, etc'
)

install="notes.install"

provides=("armcord")
conflicts=("armcord")

source=(
  "${pkgname}::git+${url}.git"
  "armcord.desktop"
  "armcord-launcher.sh"
  "notes.install"
)
sha256sums=('SKIP'
            'a964b66a4829eb2fe86a0fa4841a67a9fd3264dfea960aa4cacf2a64c0200d93'
            '8d7c4386a2709e323361b1b21dcabe57c9724f3a957ad4d8456142a3dfa7d62b'
            '238b65f6f9dc841b37327fc3611e70e871cb5cb6fdced9bab4c8ceb34052600e')

pkgver() {
	cd "$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname"
  npx pnpm install --ignore-scripts
  npm run packageQuick --optimize_for_size
}

package() {
  cd "$srcdir"

  install -Dm 644 "$pkgname/dist/"*"-unpacked/resources/app.asar" "$pkgdir/usr/share/armcord/app.asar"

  install -Dm 755 "armcord-launcher.sh" "$pkgdir/usr/bin/armcord"
  install -Dm 644 "armcord.desktop" "$pkgdir/usr/share/applications/armcord.desktop" 

  install -Dm 644 "$pkgname/build/icon.png" "$pkgdir/usr/share/pixmaps/armcord.png"
  install -Dm 644 "$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
