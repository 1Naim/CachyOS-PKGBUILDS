# vim: set ts=2 sw=2 et:
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=pacman
pkgver=6.0.2
_pkgver=6.0.2
pkgrel=8
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme' 'pacman-mirrorlist'
         'gettext' 'gawk' 'coreutils' 'gnupg' 'grep' 'debugedit')
makedepends=('meson' 'asciidoc' 'doxygen')
checkdepends=('python' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
provides=('libalpm.so')
backup=(etc/pacman.conf
        etc/makepkg.conf)
options=('strip' 'debug')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=(https://sources.archlinux.org/other/pacman/$pkgname-$_pkgver.tar.xz{,.sig}
        "pacman.conf"
        "makepkg.conf"
        "x8664v3.patch"
        "pacman-fix.patch"
        "0001-Revert-pacman-key-Don-t-check-gpg-s-trustdb-after-ea.patch"
        "0001-add-information-about-origin-db.patch"
        )
sha256sums=('7d8e3e8c5121aec0965df71f59bedf46052c6cf14f96365c4411ec3de0a4c1a5'
            'SKIP'
            '606e55f06c297d2b508bc4438890b229a1abaa68b0374a2d7f94c8e7be6792d7'
            '95154d1c82c8f8bc07458401836b6c37155a77c6ed75f54d6ae0d6e9ebb32aff'
            'a088200e67223d7cbb2b8558bbd20f909207b95132475c15e1c72d8a8fbd3a79'
            '1e62ee2639120c5b2da3a56b0174107c8d5844b8250068f1f9c2dad99e425aa5'
            'a9159166b8cf5eadebde78eeae0175493140645bc533816191d4935bba58c6d4'
            'f88756850d26b6c91cf034b52075762aee1d83e30b77b334be21e6d0ebec17a6')

prepare() {
  cd "$pkgname-$_pkgver"
  # upstream fixes
  patch -Np1 < ../pacman-fix.patch
  # Add x86-64-v3 architecture detectioon
  patch -Np1 < ../x8664v3.patch
  # Revert pacman-key commit, it seems to break the cachyos installation
  patch -Np1 < ../0001-Revert-pacman-key-Don-t-check-gpg-s-trustdb-after-ea.patch
  # Add infoormation about origin db from installed packagees
#  patch -Np1 < ../0001-add-information-about-origin-db.patch
}
build() {
  cd "$pkgname-$_pkgver"

  meson --prefix=/usr \
        --buildtype=plain \
        -Ddoc=enabled \
        -Ddoxygen=enabled \
        -Dscriptlet-shell=/usr/bin/bash \
        -Dldconfig=/usr/bin/ldconfig \
        build

  meson compile -C build
}

check() {
  cd "$pkgname-$_pkgver"

  meson test -C build
}

package() {
  cd "$pkgname-$_pkgver"

  DESTDIR="$pkgdir" meson install -C build

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
