# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-cli-installer-new
_pkgname=New-Cli-Installer
pkgver=0.8.0
pkgrel=1
pkgdesc='New CLI installer for CachyOS'
arch=('x86_64')
license=(GPL-3.0-or-later)
url="https://github.com/cachyos/new-cli-installer"
depends=('fzf' 'gawk' 'chwd' 'cachyos-rate-mirrors' 'curl')
makedepends=('cmake' 'ninja' 'git')
source=("$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('25f00577270d50df63d5ba3ca54162fba784cec849fc50200e5afcaafe2c6910f919675a073633beff8827575c4477d3870e87c2b67e5dae01429174937e4d87')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DENABLE_DEVENV=OFF \
        -DCOS_BUILD_STATIC=ON
  cmake --build build --parallel $_cpuCount
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  DESTDIR="${pkgdir}" cmake --build build --target install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 net-profiles.toml "${pkgdir}/var/lib/cachyos-installer/net-profiles.toml"
}

# vim:set sw=2 sts=2 et:
