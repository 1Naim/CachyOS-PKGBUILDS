# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
#pkgver=1.0.12504.5
#pkgver=1.0.12627.1
pkgver=1.0.12812.21
pkgrel=2
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses spirv-tools zlib)
makedepends=(git cmake clang lld llvm python libunwind spirv-headers vc-intrinsics)
options=(!emptydirs !lto)
source=(${url}/archive/igc-${pkgver}.tar.gz
        Patch15::${url}/commit/c707d1e.patch
        Patch16::${url}/commit/d1761df.patch
        Patch17::${url}/commit/e09e752.patch
        elfpackager.patch)
sha256sums=('63b013e1ec56bac17494870583182c49e2320f7a5f819a4bd632da3e6d4c752d'
            '28f218d7dc973f35892575bf7050b262d73ce847df2228afc52c5298aa5545f7'
            'e067498a8235359ae2bb955e465faf34c64f35eb2168d67890a1dd2ecbcb7b18'
            '5eb1cebbd0de3fe245ab9709cd58bc4b065bf36d7c308aa622c19cd8dd8e2e2f'
            '9f37a7f62e944488f09f3a3f5f943b06cfe35116c441ef75609f64f9e1c0366a')

prepare() {
  msg2 "15"
  patch -p1 -d ${pkgname}-igc-${pkgver} < Patch15
  msg2 "16"
  patch -p1 -d ${pkgname}-igc-${pkgver} < Patch16
  msg2 "17"
  patch -p1 -d ${pkgname}-igc-${pkgver} < Patch17
  msg2 "elf"
  patch -p1 -d ${pkgname}-igc-${pkgver} < elfpackager.patch
}
build() {
  cmake -B build -S ${pkgname}-igc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
    -DIGC_OPTION__CLANG_MODE=Prebuilds \
    -DIGC_OPTION__LLD_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_PREFERRED_VERSION='15.0.6' \
    -DIGC_OPTION__LLVM_MODE=Prebuilds \
    -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
    -DIGC_OPTION__USE_PREINSTALLED_SPRIV_HEADERS=ON \
    -DIGC_OPTION__SPIRV_TOOLS_MODE=Prebuilds \
    -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
    -DIGC_OPTION__VC_INTRINSICS_MODE=Prebuilds \
    -DINSTALL_GENX_IR=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
