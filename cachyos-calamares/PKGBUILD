# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=cachyos-calamares
pkgver=3.2.48
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64' 'x86_64_v3')
license=(GPL)
url="https://github.com/calamares/calamares"
license=('LGPL')
conflicts=('calamares')
provides=('calamares')
depends=('kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'kio' 'solid' 'yaml-cpp' 'kpmcore' 'mkinitcpio-openswap'
         'boost-libs' 'ckbcomp' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'plasma-framework'
         'qt5-xmlpatterns' 'squashfs-tools' 'libpwquality' 'efibootmgr' 'icu')
makedepends=('ninja' 'extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=(settings_online.conf
        settings_offline.conf)
source+=("calamares-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('2262f5cd9536b4e5ee1e4526c31611bce2f8a85134b3a66e2a643f7cec669104'
            'e50bccc92d2106aa8eb506688fcc0293606da58cd7b379c31fa7a477a2c70815'
            '86bdceb90eb81d42596b780ccd99385002e2b8d45d8fae836156d37b5122d020')

prepare() {
  cd ${srcdir}/calamares-${pkgver}
  sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' CMakeLists.txt

  # patches here

  # change version
  #_ver="$(cat CMakeLists.txt | grep -m3 -e "  VERSION" | grep -o "[[:digit:]]*" | xargs | sed s'/ /./g')"
  _ver="$pkgver"
  printf 'Version: %s-%s\n' "${_ver}" "${pkgrel}"
  sed -i -e "s|\${CALAMARES_VERSION_MAJOR}.\${CALAMARES_VERSION_MINOR}.\${CALAMARES_VERSION_PATCH}|${_ver}-${pkgrel}|g" CMakeLists.txt
  sed -i -e "s|CALAMARES_VERSION_RC 1|CALAMARES_VERSION_RC 0|g" CMakeLists.txt

  # change branding
  sed -i -e "s/default/cachyos/g" src/branding/CMakeLists.txt
}


build() {
  cd ${srcdir}/calamares-${pkgver}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DWITH_PYTHONQT:BOOL=ON \
        -DWITH_KF5DBus=OFF \
        -DBoost_NO_BOOST_CMAKE=ON \
        -DWEBVIEW_FORCE_WEBKIT=OFF \
        -DSKIP_MODULES="tracking webview interactiveterminal initramfs \
                        initramfscfg dracut dracutlukscfg \
                        dummyprocess dummypython dummycpp welcomeq \
                        keyboardq localeq \
                        dummypythonqt services-openrc"
  cmake --build build --parallel $_cpuCount
}

package() {
  cd ${srcdir}/calamares-${pkgver}/build
  DESTDIR="${pkgdir}" cmake --build . --target install

  #cp ${srcdir}/settings_offline.conf "$pkgdir/etc/calamares/settings.conf"
  cp ${srcdir}/settings_offline.conf "$pkgdir/usr/share/calamares/settings_offline.conf"
  cp ${srcdir}/settings_online.conf "$pkgdir/usr/share/calamares/settings_online.conf"
}

# vim:set sw=2 sts=2 et:
