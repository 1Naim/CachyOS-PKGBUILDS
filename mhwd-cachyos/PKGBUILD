# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Contributer : Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributer : Roland Singer <roland[at]manjaro[dog]org>

pkgbase=mhwd-cachyos
pkgname=('mhwd-cachyos' 'mhwd-db-cachyos')
pkgname=mhwd-cachyos
pkgver=0.7.0
pkgrel=2
pkgdesc="Hardware Detection for CachyOS"
arch=(x86_64 x86_64_v3)
url="https://github.com/cachyos/mhwd-cachyos"
license=(GPLv3)
depends=('gcc-libs' 'hwinfo' 'lua')
makedepends=('cmake' 'ninja' 'git' 'rust' 'mold' 'clang' 'llvm')
groups=('cachyos')
_commit_hash=c3eaf6f41735af4d183c537e95b8c505977641bb
source=("git+${url}.git#commit=${_commit_hash}"
        "git+${url/-cachyos}-db-cachyos.git")
sha256sums=('SKIP'
            'SKIP')
options=(!strip)

build() {
  cd $pkgname

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  cd scripts/chwd-kernel

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/mold"
  cargo build --release

  cd ../../

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)
  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_EXE_LINKER_FLAGS="-fuse-ld=mold"
  cmake --build build --parallel $_cpuCount
}

package_mhwd-cachyos() {
  pkgdesc="mhwd-cachyos library and application"
  depends=('hwinfo' 'mhwd-db-cachyos' 'pacman')
  provides=('mhwd' 'mhwd-cachyos')
  conflicts=('mhwd' 'mhwd-cachyos')

  cd $pkgname

  install -Dm755 ./bin/mhwd "$pkgdir"/usr/bin/mhwd
  install -Dm755 ./scripts/chwd "$pkgdir"/var/lib/mhwd/scripts/chwd
  install -Dm755 ./scripts/mhwd-fb "$pkgdir"/usr/bin/mhwd-fb

  install -Dm755 ./scripts/initcpio/hooks/mhwd-fb "$pkgdir"/usr/lib/initcpio/hooks/mhwd-fb
  install -Dm755 ./scripts/initcpio/install/mhwd-fb "$pkgdir"/usr/lib/initcpio/install/mhwd-fb

  install -d -m755 "$pkgdir"/var/lib/mhwd/{db,local}/{pci,usb}

  cp ./scripts/chwd-kernel/target/release/chwd-kernel "$pkgdir"/usr/bin/
}

package_mhwd-db-cachyos() {
  pkgdesc="mhwd-cachyos Database"
  depends=('libnotify' 'mhwd-nvidia' 'mhwd-nvidia>=525' 'mhwd-nvidia-470xx' 'mhwd-nvidia-390xx')
  provides=('mhwd-db' 'mhwd-db-cachyos')
  conflicts=('mhwd-db' 'mhwd-db-cachyos')

  cd $pkgname

  mkdir -p "$pkgdir"/var/lib/mhwd/db
  mkdir -p "$pkgdir"/etc/X11/mhwd.d
  cp -r pci "$pkgdir"/var/lib/mhwd/db/
}

# vim:set sw=2 sts=2 et:
