# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Contributer : Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributer : Roland Singer <roland[at]manjaro[dog]org>

pkgbase=mhwd-cachyos
pkgname=('mhwd-cachyos' 'mhwd-db-cachyos')
pkgname=mhwd-cachyos
pkgver=0.6.14
pkgrel=1
pkgdesc="Hardware Detection for CachyOS"
arch=(x86_64 x86_64_v3)
url="https://github.com/cachyos/mhwd-cachyos"
license=(GPLv3)
depends=('gcc-libs' 'hwinfo')
makedepends=('cmake' 'ninja' 'git' 'rust' 'mold' 'clang')
groups=('cachyos')
_commit_hash=b80a078a75d6fb3598603028654b35612d716479
source=("git+${url}.git#commit=${_commit_hash}"
        "git+${url/-cachyos}-db-cachyos.git")
sha256sums=('SKIP'
            'SKIP')
options=(!strip)

build() {
  cd $pkgname

  cd scripts/chwd-kernel

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/mold"
  cargo build --release

  cd ../../

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)
  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build --parallel $_cpuCount
}

package_mhwd-cachyos() {
  pkgdesc="mhwd-cachyos library and application"
  depends=('hwinfo' 'mhwd-db-cachyos' 'pacman')
  provides=('mhwd' 'mhwd-cachyos')
  conflicts=('mhwd' 'mhwd-cachyos')
  install=mhwd.install

  cd $pkgname/build

  DESTDIR="${pkgdir}" ninja install
  install -d -m755 "$pkgdir"/var/lib/mhwd/{db,local}/{pci,usb}

  cp ../scripts/chwd-kernel/target/release/chwd-kernel "$pkgdir"/usr/bin/
}

package_mhwd-db-cachyos() {
  pkgdesc="mhwd-cachyos Database"
  depends=('libnotify' 'mhwd-nvidia' 'mhwd-nvidia>=525' 'mhwd-nvidia-470xx' 'mhwd-nvidia-390xx')
  provides=('mhwd-db' 'mhwd-db-cachyos')
  conflicts=('mhwd-db' 'mhwd-db-cachyos')
  install=mhwd-db.install

  cd $pkgname

  mkdir -p "$pkgdir"/var/lib/mhwd/db
  mkdir -p "$pkgdir"/etc/X11/mhwd.d
  cp -r pci "$pkgdir"/var/lib/mhwd/db/
}

# vim:set sw=2 sts=2 et:
