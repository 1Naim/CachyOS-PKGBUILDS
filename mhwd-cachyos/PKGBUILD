# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgbase=mhwd-cachyos
pkgname=('mhwd-cachyos' 'mhwd-db-cachyos')
pkgname=mhwd-cachyos
pkgver=0.8.0
pkgrel=1
pkgdesc="Hardware Detection for CachyOS"
arch=(x86_64)
url="https://github.com/cachyos/mhwd-cachyos"
license=(GPL3)
depends=('gcc-libs' 'pciutils' 'lua')
makedepends=('git' 'cargo' 'mold' 'clang' 'llvm')
groups=('cachyos')
_commit_hash=c27d61fe52862da841986d713899ccb6b7f45d92
source=("git+${url}.git#commit=${_commit_hash}"
        "git+${url/-cachyos}-db-cachyos.git")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"

  cd scripts/chwd-kernel
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$pkgname"

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/mold"

  cd scripts/chwd-kernel
  cargo build --release --frozen

  cd ../../

  cargo build --release --frozen
}

package_mhwd-cachyos() {
  depends=('gcc-libs' 'pciutils' 'lua' 'mhwd-db-cachyos' 'pacman')
  provides=('mhwd-cachyos')
  conflicts=('mhwd-cachyos')

  cd "$srcdir/$pkgname"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/chwd/LICENSE"

  install -Dm755 "target/release/chwd" "$pkgdir/usr/bin/chwd"
  ln -s "/usr/bin/chwd" "$pkgdir/usr/bin/mhwd"
  install -Dm755 scripts/chwd "$pkgdir"/var/lib/mhwd/scripts/chwd

  install -Dm644 target/release/build/chwd-*/out/chwd.bash "$pkgdir/usr/share/bash-completion/completions/chwd"
  install -Dm644 target/release/build/chwd-*/out/chwd.fish "$pkgdir/usr/share/fish/vendor_completions.d/chwd.fish"
  install -Dm644 target/release/build/chwd-*/out/_chwd "$pkgdir/usr/share/zsh/site-functions/_chwd"

  install -d -m755 "$pkgdir"/var/lib/mhwd/{db,local}/pci

  cd scripts/chwd-kernel
  install -Dm755 "target/release/chwd-kernel" "$pkgdir/usr/bin/chwd-kernel"
}

package_mhwd-db-cachyos() {
  pkgdesc="mhwd-cachyos Database"
  depends=('mhwd-nvidia' 'mhwd-nvidia>=525' 'mhwd-nvidia-470xx' 'mhwd-nvidia-390xx')
  provides=('mhwd-db-cachyos')
  conflicts=('mhwd-db-cachyos')

  cd $pkgname

  mkdir -p "$pkgdir"/var/lib/mhwd/db
  cp -r pci "$pkgdir"/var/lib/mhwd/db/
}

# vim:set sw=2 sts=2 et:
