# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kyuzial <kyuzial@protonmail.com>

pkgname=cosmic-terminal
pkgver=1.0.0.alpha.2
pkgrel=2
pkgdesc='Cosmic Terminal Emulator'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-term
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
  cosmic-icon-theme
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_tag=2e2a2376dfe0c6959e4e7d115f7b7d19405737ad
source=(git+https://github.com/pop-os/cosmic-term.git#tag=${_tag})
b2sums=('5dd518b9824428279b0645dd4b535581dacba42f76459e087f89fa2193c36c71b850864419dde7cea11cd63149bafceff09b6319594c92d8ce08f9cc0f0207dd')

prepare() {
  cd cosmic-term
  cargo fetch --locked
}

pkgver() {
  cd cosmic-term
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-term
  export CC=clang
  export CXX=clang++
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-term
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
