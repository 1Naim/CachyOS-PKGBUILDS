# Maintainer: Peter Jung <ptr1337@cachyos.org>
# Contributor: soloturn <soloturn@gmail.com>
# Co-Maintainer: Mark Wagie <mark dot wagie at proton dot me>

pkgname=cosmic-applets
pkgver=1.0.0.alpha.2
pkgrel=2
pkgdesc="WIP applets for COSMIC Panel"
arch=('x86_64')
url="https://github.com/pop-os/cosmic-applets"
license=('GPL-3.0-or-later')
groups=('cosmic')
depends=(
  'cosmic-icon-theme'
  'dbus'
  'libinput'
  'libpulse'
  'libxkbcommon'
)
makedepends=(
  'cargo'
  clang
  'git'
  'just'
  'lld'
)
options=('!lto')
_tag=3670f124fb51ff730c470398014a38ee166e2dbb
source=("git+https://github.com/pop-os/cosmic-applets.git#tag=${_tag}")
sha256sums=('6172635cf93c66fb1e1ca26962bc832ce31b9233171d11dc638ed4c200d35e35')

prepare() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable

  export CC=clang
  export CXX=clang++
  # use lld
  RUSTFLAGS="-C link-arg=-fuse-ld=lld"

  # use nice to build with lower priority
  nice just build-release --frozen
}

package() {
  cd "${pkgname}"
  just rootdir="$pkgdir" install
}
