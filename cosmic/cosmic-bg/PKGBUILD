# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-bg
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='COSMIC session service which applies backgrounds to displays'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-bg
license=(MPL-2.0)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
  nasm
)
_tag=584f6b3c0454396df25d36c6c2b59b018946e81e
source=(git+https://github.com/pop-os/cosmic-bg.git#tag=${_tag})
b2sums=('0306af059b179b3ff3bcc91965111205dacdf4097d085022d3c189b4ceb678c67a710de6ed936e93373e1826fcc228a2bf92e5c0eab6869962a19b4365adcb3e')

prepare() {
  cd cosmic-bg
  cargo fetch --locked
}

pkgver() {
  cd cosmic-bg
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-bg
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-bg
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
