# Maintainer: Peter Jung <ptr1337@cachyos.org>
# Maintainer: Mark Wagie <mark dot wagie at proton dot me>

pkgname=cosmic-files
pkgver=1.0.0.alpha.1
pkgrel=1
pkgdesc="File manager for the COSMIC desktop environment"
arch=('x86_64')
url="https://github.com/pop-os/cosmic-files"
license=('GPL-3.0-or-later')
groups=('cosmic')
depends=(
  'glib2'
  'hicolor-icon-theme'
  'libxkbcommon'
  'xdg-utils'
)
makedepends=(
  'cargo'
  'git'
  'just'
  'lld'
)
source=('git+https://github.com/pop-os/cosmic-files.git#tag=epoch-1.0.0-alpha.1')
sha256sums=('f409be729e03b6cd2396551a7bcd3a030fd630089254086d15f2a4ed94d50ebb')

prepare() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable

  # use lld
  RUSTFLAGS="-C link-arg=-fuse-ld=lld"

  # use nice to build with lower priority
  nice just build-release --frozen
}

package() {
  cd "${pkgname}"
  just rootdir="$pkgdir" install
}
