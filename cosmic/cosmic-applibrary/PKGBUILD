# Maintainer: Peter Jung <ptr1337@cachyos.org>
# Contributor: soloturn <soloturn@gmail.com>
# Co-Maintainer: Mark Wagie <mark dot wagie at proton dot me>

pkgname=cosmic-applibrary
pkgver=1.0.0.alpha.1
pkgrel=3
pkgdesc="WIP Cosmic App Library"
arch=('x86_64')
url="https://github.com/pop-os/cosmic-applibrary"
license=('GPL-3.0-or-later')
groups=('cosmic')
depends=(
  'hicolor-icon-theme'
  'libxkbcommon')
makedepends=(
  'cargo'
  'git'
  'just'
  'lld'
)
options=('!lto')
source=('git+https://github.com/pop-os/cosmic-applibrary.git#tag=epoch-1.0.0-alpha.1')
sha256sums=('fa4e68a67fa5f4b5cdcfc2d24ad1847aced6e1bd690823070471dc88d89ecfcb')

prepare() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable

  # use lld
  RUSTFLAGS="-C link-arg=-fuse-ld=lld"

  # use nice to build with lower priority
  nice just build-release --frozen
}

package() {
  cd "${pkgname}"
  just rootdir="$pkgdir" install
}
