# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-settings-daemon
pkgver=1.0.0.alpha.2
pkgrel=2
pkgdesc='Cosmic settings daemon'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-settings-daemon
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  acpid
  alsa-utils
  adw-gtk-theme
  gcc-libs
  geoclue
  glibc
  libinput
  playerctl
  pop-sound-theme
  systemd-libs
)
makedepends=(
  cargo
  clang
  git
  lld
)
_tag=a41d1991fdfdac58f0b788e43087ad6ee01d00ca
source=(git+https://github.com/pop-os/cosmic-settings-daemon.git#tag=${_tag})
b2sums=('38c0445581be3c5867928050c6ddce100fccd79b700038086507909a3642cb2183032a829f6330725d034a08788e849b9f2b70027ecc470ef70b99f2c3f129e3')

prepare() {
  cd cosmic-settings-daemon
  cargo fetch --locked
  sed 's/libexec/lib/g' -i Makefile src/main.rs
  sed 's/sudo/wheel/g' -i data/polkit-1/rules.d/cosmic-settings-daemon.rules
}

pkgver() {
  cd cosmic-settings-daemon
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-settings-daemon
  export CC=clang
  export CXX=clang++
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-settings-daemon
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
