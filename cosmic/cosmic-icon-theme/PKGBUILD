# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-icon-theme
pkgver=1.0.0.alpha.1
pkgrel=4
pkgdesc='System76 Cosmic icon theme'
arch=(any)
url=https://github.com/pop-os/cosmic-icons
license=(CC-BY-SA-4.0)
depends=(pop-icon-theme)
makedepends=(
  git
  just
)
options=(!strip)
provides=('cosmic-icons')
replaces=('cosmic-icons')
_tag=f93dcdfa1060c2cf3f8cf0b56b0338292edcafa5
source=(git+https://github.com/pop-os/cosmic-icons.git#tag=${_tag})
b2sums=('d9761fb7b2e6c9f30cebc6cd895204e14073a299580a73ba44a6aca580fbed86aec598d3fc54a63392fa327aa5448ddd2b98ab8cf69ab2ad8673c97944caa0c9')

pkgver() {
  cd cosmic-icons
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

package() {
  cd cosmic-icons
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
