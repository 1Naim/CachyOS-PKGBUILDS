# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-workspaces
pkgver=1.0.0.alpha.2
pkgrel=2
pkgdesc='Cosmic workspaces'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-workspaces-epoch
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libinput
  libxkbcommon
  mesa
  wayland
)
makedepends=(
  cargo
  clang
  git
  lld
)
_tag=70d6c415a1cd931f575c3e18274c3fecc7cc9a03
source=(git+https://github.com/pop-os/cosmic-workspaces-epoch.git#tag=${_tag})
b2sums=('06c1eeb25d12287c0c9865543a5bf8bf1fd01d338edea07b2d20ef3783f77ffb43fcfd1550786f2703ffdcdd833a60c39f8beaa4dc59792ca4d825e6b5ac0b88')

prepare() {
  cd cosmic-workspaces-epoch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-workspaces-epoch
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-workspaces-epoch
  export CC=clang
  export CXX=clang++
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-workspaces-epoch
  make prefix='/usr' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
