# Maintainer: Peter Jung <ptr1337@cachyos.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=cosmic-comp
pkgver=1.0.0.alpha.1
pkgrel=3
pkgdesc="Compositor for the COSMIC desktop environment"
arch=('x86_64')
url="https://github.com/pop-os/cosmic-comp"
license=('GPL-3.0-or-later')
groups=('cosmic')
depends=(
  'fontconfig'
  'libseat.so'
  'libinput'
  'libxcb'
  'libxkbcommon'
  'mesa'
  'pixman'
  'systemd'
  'wayland'
)
makedepends=(
  'cargo'
  'git'
  'lld'
)
source=('git+https://github.com/pop-os/cosmic-comp.git#tag=epoch-1.0.0-alpha.1')
sha256sums=('bafefcc583f7756d4cec1738ca8609d2f2dfae1192f28c701087ab6afe65dec2')

prepare() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  msg2 "Cherrypick newer Smithay to fix regression"
  git cherry-pick -n 0916d3599a91fe40503209e0431fcc888fa6b867
  make vendor
}

build() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable

  # use lld
  RUSTFLAGS="-C link-arg=-fuse-ld=lld"

  # use nice to build with lower priority
  nice make VENDOR='1' all
}

package() {
  cd "${pkgname}"
  make DESTDIR="$pkgdir" install
}
