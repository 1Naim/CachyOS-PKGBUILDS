# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>

pkgname=cachyos-st-config
pkgver=0.8.5
pkgrel=3
pkgdesc='CachyOS ST'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
license=('MIT')
depends=(libxft libxcursor fontconfig harfbuzz ttf-hack ttf-fira-code ttf-jetbrains-mono)
makedepends=('git')
url="https://github.com/cachyos/$pkgname"
_commit=92664bd9598047d9d41558aea2966e7fac4a0c30
source=(git+$url.git#commit=${_commit})
sha256sums=('SKIP')
provides=('st')
conflicts=('st')
_sourcedir=cachyos-st-config
_makeopts="--directory=$_sourcedir"

build() {
  make $_makeopts X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  local installopts='--mode 0644 -D --target-directory'
  local shrdir="$pkgdir/usr/share"
  local licdir="$shrdir/licenses/st"
  local docdir="$shrdir/doc/st"
  make $_makeopts PREFIX=/usr DESTDIR="$pkgdir" install
  install $installopts "$licdir" "$_sourcedir/LICENSE"
  install $installopts "$docdir" "$_sourcedir/README"
  install $installopts "$shrdir/st" "$_sourcedir/st.info"
}
