# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-hello
_pkgname=CachyOS-Welcome
pkgver=0.14.5
pkgrel=1
pkgdesc='Welcome screen for CachyOS'
arch=('x86_64' 'x86_64_v3')
groups=('cachyos')
license=(GPL-3.0-or-later)
url="https://github.com/cachyos/cachyos-welcome"
depends=('gtk3' 'glib2' 'qt6-tools' 'procps-ng')
makedepends=('meson' 'git' 'mold' 'rustup' 'clang')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('80801fa0cc427c3ff8f348108fbd5092ce77878ed537ffc59eb1078cf7a03ffe6a2bfad202d683181b921c5f9b6eb6514bd305d45b8e51e973168a0d0501d6b8')
provides=('cachyos-hello')
conflicts=('cachyos-hello')
options=(strip)

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if ! rustc --version | grep nightly >/dev/null 2>&1; then
    echo "Installing nightly compilerâ€¦"
    rustup toolchain install nightly
    rustup default nightly
  fi

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/mold"
  meson setup --buildtype=release --prefix=/usr build

  meson compile -C build --jobs $_cpuCount
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"/build

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/mold"
  DESTDIR="${pkgdir}" meson install

  install -Dvm644 ../$pkgname.desktop \
    "$pkgdir/etc/skel/.config/autostart/$pkgname.desktop"
}

# vim:set sw=2 sts=2 et:
