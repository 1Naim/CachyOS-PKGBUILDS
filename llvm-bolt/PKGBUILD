

pkgname=('llvm-bolt')
pkgver=18.0.0_r479950.002f42241080
pkgrel=1
arch=('x86_64')
url="https://llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
makedepends=('git' 'jemalloc')
source=("llvm-project::git+https://github.com/llvm/llvm-project.git"
        "llvm-config.h")
b2sums=('SKIP'
            '75e743dea28b280943b3cc7f8bbb871b57d110a7f2b9da2e6845c1c36bf170dd883fca54e463f5f49e0c3effe07fbd0db0f8cf5a12a2469d3f792af21a73fcdd')
options=('staticlibs')

_python_optimize() {
  python -m compileall "$@"
  python -O -m compileall "$@"
  python -OO -m compileall "$@"
}

pkgver() {
    cd llvm-project/llvm

    # This will almost match the output of `llvm-config --version` when the
    # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
    # dash being replaced with underscore because of Pacman requirements.
    local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
            'BEGIN { ORS="." ; i=0 } \
             /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
             END { print "\n" }' \
             CMakeLists.txt)_r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
    echo "$_pkgver"
}

build() {
    
    export CFLAGS+=" ${CPPFLAGS}"
    export CXXFLAGS+=" ${CPPFLAGS}"
    export CXXFLAGS+=" -stdlib=libstdc++"
    cmake \
        -B _build \
        -S "$srcdir"/llvm-project/llvm  \
        -G Ninja \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D LLVM_BINUTILS_INCDIR=/usr/include \
        -D LLVM_VERSION_SUFFIX="" \
        -D LLVM_HOST_TRIPLE=$CHOST \
        -D LLVM_INSTALL_UTILS=OFF \
        -D LLVM_BUILD_DOCS=OFF \
        -D LLVM_TARGETS_TO_BUILD="X86;AArch64" \
        -D LLVM_ENABLE_PROJECTS="bolt" \
        -D LLVM_ENABLE_ASSERTIONS=ON \
	    -D CMAKE_EXE_LINKER_FLAGS="-Wl,--push-state -Wl,-whole-archive -ljemalloc_pic -Wl,--pop-state -lpthread -lstdc++ -lm -ldl " \
        -D LLVM_LIT_ARGS="-sv --ignore-fail" \
        -Wno-dev

    ninja -C _build $NINJAFLAGS bolt
}

package_llvm-bolt() {
    pkgdesc="LLVM Bolt"
    depends=()
    provides=(llvm-bolt)
    conflicts=()
    
    DESTDIR="$pkgdir" ninja -C _build $NINJAFLAGS install-bolt
#    rm -rf "$pkgdir"/home

    install -Dm644 "$srcdir"/llvm-project/bolt/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/bolt-LICENSE
}
