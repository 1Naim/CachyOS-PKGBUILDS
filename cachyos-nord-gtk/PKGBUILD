# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>

pkgname=cachyos-nord-gtk-theme-git
pkgver=r10.18b1535
pkgrel=1
groups=(cachyos)
pkgdesc="Nord-derived themes for GTK and other platforms"
arch=("any")
url="https://github.com/CachyOS/CachyOS-Nord-KDE"
license=(GPL-3.0-or-later)
makedepends=('git')
source=("$pkgname::git+$url")
sha256sums=('SKIP')

pkgver() {
   cd "$pkgname"/gtk/themes/cachyOS-nord
   printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "$pkgname"/gtk/themes/cachyOS-nord
    find assets cinnamon openbox-3 gtk* metacity-1 unity xfwm4 \
        -type f \
        -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/cachyos-nord/{}" \;
}
