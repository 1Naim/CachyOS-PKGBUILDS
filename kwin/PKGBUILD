# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwin
pkgver=6.1.1.2
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=6
pkgdesc='An easy to use, but flexible, composited Window Manager (CachyOS Version)'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(breeze
         gcc-libs
         glibc
         plasma-activities
         kauth
         kcmutils
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kdeclarative
         kdecoration
         kglobalaccel
         kglobalacceld
         kguiaddons
         ki18n
         kidletime
         kirigami
         kitemmodels
         knewstuff
         knotifications
         kpackage
         kquickcharts
         kscreenlocker
         kservice
         ksvg
         kwayland
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         lcms2
         libdisplay-info
         libdrm
         libei
         libepoxy
         libinput
         libpipewire
         libqaccessibilityclient-qt6
         libx11
         libxcb
         libxcvt
         libxi
         libxkbcommon
         libxkbcommon-x11
         mesa
         pipewire-session-manager
         libplasma
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-sensors
         qt6-tools
         qt6-wayland
         systemd-libs
         wayland
         xcb-util-cursor
         xcb-util-keysyms
         xcb-util-wm)
makedepends=(extra-cmake-modules
             kdoctools
             krunner
             plasma-wayland-protocols
             python
             wayland-protocols
             xorg-xwayland)
optdepends=('maliit-keyboard: virtual keyboard for kwin-wayland')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
		0001-backends-drm-support-tearing-with-atomic-modesetting.patch::https://invent.kde.org/plasma/kwin/-/commit/155c29b08f243c654630869e11a588e28d8199b1.patch
		https://invent.kde.org/plasma/kwin/-/merge_requests/6016.patch
		https://invent.kde.org/plasma/kwin/-/merge_requests/5986.patch
		489117.patch::https://invent.kde.org/plasma/kwin/-/commit/deeeddb652d20086625a221788dff1926b26172f.patch
		menubuttonfix.patch::https://invent.kde.org/plasma/kwin/-/commit/176e338c370bcee12037b34ae5ffaf2d3bcb8ec8.patch
		https://invent.kde.org/plasma/kwin/-/merge_requests/5982.patch)
install=$pkgname.install
sha256sums=('1cf0e4d996ccd92a77b6a725610ca32a60c2fae08a98f8b72f2aaf1569464c39'
            'SKIP'
            '2642d2faa03a1777c3839ba71fb79c79963348a46e83f6fc024126085ee86301'
            '1b061a86f93814c6c27c5b27bdbb7034bcb08660b0775db8cf02f616eaf72011'
            '6dc2df5d65d85f5147c8f1dcb986d752c593029db732bf66aac2bfcafcb9e344'
            'f8d364f3ca499e2438cf7a7174e84d1491626942e7937028df1dd43a3e517f0f'
            '8115bceb0005fba4b48157253f04e55088f33c85fad66250f233372d05bb7055'
            'de3cde7a6a77a950a96c15eb8861506c2be997a129eede1d23670e0a7b40e2ae')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
prepare() {
  cd $pkgname-$pkgver
  # Tearing
  patch -Np1 < ../0001-backends-drm-support-tearing-with-atomic-modesetting.patch
  # Increase Max buffer size
  patch -Np1 < ../6016.patch
  # core/renderloop: add some hysteresis to triple buffering
  patch -Np1 < ../5982.patch
}

build() {
  cmake -B build  -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
