# Maintainer: Adrian Perez de Castro <aperez@igalia.com>

pkgname=wcm
pkgdesc='Wayfire Configuration Manager'
pkgver=0.8.0
pkgrel=5
arch=(x86_64)
url=https://wayfire.org
license=(custom:MIT)
depends=("wayfire>=${pkgver%.*}" 'wayfire<0.9' "wf-shell>=${pkgver%.*}" 'wf-shell<0.9' 'gtkmm3' 'wdisplays')
makedepends=('wayland-protocols' 'meson' 'ninja' 'glm')
source=("https://github.com/WayfireWM/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4b7c847f48e80bad3448961b9900a1f4eed8df101daa033592b60dc892f253c9')

build () {
  cd "${srcdir}/$pkgname-$pkgver"
  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  PKG_CONFIG_PATH=/usr/lib/wlroots0.16/pkgconfig \
  arch-meson --auto-features=disabled -Dwf_shell=enabled -Denable_wdisplays=false build
  meson compile -C build --jobs $_cpuCount
}

check () {
  cd "${srcdir}/$pkgname-$pkgver"
  meson test -C build
}

package () {
  cd "${srcdir}/$pkgname-$pkgver/build"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}/$pkgname-$pkgver"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
