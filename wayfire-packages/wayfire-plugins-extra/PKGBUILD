# Maintainer: Adrian Perez de Castro <aperez@igalia.com>

pkgname=wayfire-plugins-extra
pkgver=0.8.0
pkgrel=2
pkgdesc='Additional plugins for Wayfire'
url=https://github.com/WayfireWM/wayfire-plugins-extra
arch=(x86_64)
license=(custom:MIT)
conflicts=("${pkgname}-git")
depends=('wayfire>=0.8' 'cairo' 'glibmm' 'wlroots0.16' 'iio-sensor-proxy')
makedepends=('meson' 'wayland-protocols' 'ninja' 'glm' 'git' 'cmake')
_commit=5076dc1100071d8ed220e06a4723bde16ac864f7
source=("https://github.com/WayfireWM/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "fix-build.patch")
sha512sums=('9cc816c3c727b0093fd15ef52dea215598b4c49ef96c098f53938c041b3908b30f4dd616be4b459b7382d630a47b36baf845c24de84f35670ba2912711205911'
            'b0283ccce25f717c0bdfef868bb4830241c696f2536de656ca68df01dcc5ab8e57ca877348e8c2f7dfca4e7389096e4a9990ac20c0fc16b5e6a3d3fa34ba4624')

#prepare() {
#  cd "${srcdir}/$pkgname-$pkgver"
#  patch -Np1 -i "$srcdir/fix-build.patch"
#}

build () {
  cd "${srcdir}/$pkgname-$pkgver"
  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  PKG_CONFIG_PATH=/usr/lib/wlroots0.16/pkgconfig \
  arch-meson build
  meson compile -C build --jobs $_cpuCount
}

check () {
  cd "${srcdir}/$pkgname-$pkgver"
  meson test -C build
}

package () {
  cd "${srcdir}/$pkgname-$pkgver/build"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}/$pkgname-$pkgver"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
