# Maintainer: Adrian Perez de Castro <aperez@igalia.com>

pkgname=wayfire
pkgver=0.8.0
pkgrel=1
pkgdesc="3D wayland compositor"
arch=('x86_64')
url=https://wayfire.org
license=(custom:MIT)
depends=('cairo' 'pango' 'wf-config>=0.8' 'libjpeg' 'libinput' 'wlroots>=0.16' 'wlroots<0.17')
makedepends=('meson' 'ninja' 'wayland-protocols' 'glm' 'cmake' 'doctest' 'doxygen' 'nlohmann-json')
conflicts=("${pkgname}-git")
source=("https://github.com/WayfireWM/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('fce6d4c81fa3675df1e656f8bf504f7fc9d1b7104687bcc6d669950582f46bd0ad084c6a487434077df029438243cd91cf6ec60dc7d584b40bfd7a4c8d069f53')
options=(strip)

build() {
  cd "${pkgname}-${pkgver}"
  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  arch-meson -Duse_system_wfconfig=enabled \
             -Duse_system_wlroots=enabled \
             -Dxwayland=enabled \
             build

  meson compile -C build --jobs $_cpuCount
}

check () {
  cd "${pkgname}-${pkgver}"
  meson test -C build --print-errorlogs
}

package() {
  cd "${pkgname}-${pkgver}/build"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm644 wayfire.desktop "${pkgdir}/usr/share/wayland-sessions/wayfire.desktop"
  install -Dm644 wayfire.ini "${pkgdir}/usr/share/doc/${pkgname}/wayfire.ini"
  install -Dm645 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
