# Maintainer: Damian N. <nycko123 at gmail>
# Previous Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=cachyos-bspwm
_pkgname=bspwm
pkgver=0.9.10
pkgrel=3.1
pkgdesc='Tiling window manager based on binary space partitioning - with rounded corners'
arch=('any')
url='https://github.com/baskerville/bspwm'
license=(BSD)
makedepends=(git)
depends=('xcb-util'
        'xcb-util-wm'
        'xcb-util-keysyms'
        'cachyos-zsh-config'
        'rofi'
        'rxvt-unicode'
        # 'lightdm-webkit2-greeter'
        'cachyos-picom-config'
        'polybar'
        'sxhkd'
        'deadd-notification-center'
        'ttf-fira-sans')
install=$pkgname.install
source=("git+$url#commit=2492695ad47c7ae8c630bdf22328334f57e41c1a"
        "rounded-corners.patch::https://github.com/j-james/bspwm-rounded-corners/commit/00e85df0505d2bfbd41234d1daf2f03c461b6a2b.patch")
sha512sums=('SKIP'
            '74820b5c2289b9b9c09a782dd43c2e30e82ac6fe775531656149712f91d0285a55bf632c8d7116ff82ef789c6643ab851f74fc916daf2b3c333260f140db4491')
options=('!debug' 'strip')
provides=('bspwm')
conflicts=('bspwm' 'bspwm-git')
replaces=('bspwm')

prepare() {
  patch -d "$_pkgname" -p1 -i ../rounded-corners.patch
}

build() {
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make -C "$_pkgname" PREFIX=/usr
}

package() {
  cd "$_pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install

  # BSD 2-clause
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}

# getver: -u 2 raw.githubusercontent.com/baskerville/bspwm/master/doc/bspwm.1
# vim: ts=2 sw=2 et:
