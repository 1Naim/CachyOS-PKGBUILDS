# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>

pkgbase=cachyos-mirrorlist
pkgname=('cachyos-mirrorlist' 'cachyos-v3-mirrorlist' 'cachyos-v4-mirrorlist')
pkgver=22
pkgrel=1
pkgdesc="CachyOS repository mirrorlist"
arch=('any')
license=(GPL-3.0-or-later)
groups=(cachyos)
source=(cachyos-mirrorlist)
sha512sums=('237404b5bff674904acd97b7d8599c8b5e33db2a9dd349646006b744f492b052d9488374b3c655a8a8f0599a00d9680750b9470c648c00e5cb9ac6ceb7801b98')

package_cachyos-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v3-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v3/|g' "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v4-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v4/|g' "$pkgdir/etc/pacman.d/$pkgname"
}
