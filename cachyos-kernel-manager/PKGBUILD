# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-kernel-manager
pkgver=1.0.9
pkgrel=1
pkgdesc="Simple kernel manager"
arch=(aarch64 x86_64 x86_64_v3)
url="https://github.com/cachyos/kernel-manager"
license=(GPLv3)
depends=('qt5-base' 'polkit-qt5')
makedepends=('cmake' 'ninja' 'git' 'python')
groups=('cachyos')
source=("kernel-manager-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('f2f4577c8a6f67e4e56d93ac96420101d27cec0ed07afe99d15c5a95b86db2a8')
provides=('cachyos-kernel-manager')
conflicts=('cachyos-kernel-manager')
options=(strip)

build() {
  cd ${srcdir}/kernel-manager-${pkgver}

  CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build --parallel $_cpuCount
}

package() {
  cd ${srcdir}/kernel-manager-${pkgver}
  DESTDIR="${pkgdir}" cmake --build build --target install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
