# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-kernel-manager
pkgver=1.5.1
pkgrel=2
pkgdesc="Simple kernel manager"
arch=(aarch64 x86_64 x86_64_v3)
url="https://github.com/cachyos/kernel-manager"
license=(GPLv3)
depends=('qt6-base' 'polkit')
makedepends=('cmake' 'ninja' 'git' 'python' 'polkit-qt6' 'qt6-tools')
groups=('cachyos')
source=("kernel-manager-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        fix-installed-db.patch)
sha256sums=('34229ad1f107107c66cced26ae35da5e6e7d0e57433a158c68f060710724b2f9'
            '455c136513110b88e8234b8e4b25fd739804122ba6a492cde369b1f76c90960c')
provides=('cachyos-kernel-manager')
conflicts=('cachyos-kernel-manager')
options=(strip)

prepare() {
  cd ${srcdir}/kernel-manager-${pkgver}

  patch -Np1 -i ../fix-installed-db.patch
}

build() {
  cd ${srcdir}/kernel-manager-${pkgver}

  CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build --parallel $_cpuCount
}

package() {
  cd ${srcdir}/kernel-manager-${pkgver}
  DESTDIR="${pkgdir}" cmake --build build --target install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
