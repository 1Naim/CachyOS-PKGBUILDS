# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-kernel-manager
pkgver=1.4.2
pkgrel=1
pkgdesc="Simple kernel manager"
arch=(aarch64 x86_64 x86_64_v3)
url="https://github.com/cachyos/kernel-manager"
license=(GPLv3)
depends=('qt5-base' 'polkit')
makedepends=('cmake' 'ninja' 'git' 'python' 'polkit-qt5' 'qt5-tools')
groups=('cachyos')
source=("kernel-manager-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        fix-installed-db.patch)
sha256sums=('13a76b8d249d3dd11479c1488b907ec35b47e55f960f56e41cade61876d0e88b'
            '455c136513110b88e8234b8e4b25fd739804122ba6a492cde369b1f76c90960c')
provides=('cachyos-kernel-manager')
conflicts=('cachyos-kernel-manager')
options=(strip)

prepare() {
  cd ${srcdir}/kernel-manager-${pkgver}

  patch -Np1 -i ../fix-installed-db.patch
}

build() {
  cd ${srcdir}/kernel-manager-${pkgver}

  CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build --parallel $_cpuCount
}

package() {
  cd ${srcdir}/kernel-manager-${pkgver}
  DESTDIR="${pkgdir}" cmake --build build --target install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
