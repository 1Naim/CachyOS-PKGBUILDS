# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-kernel-manager
pkgver=0.9.3
pkgrel=3
pkgdesc="Simple kernel manager"
arch=(aarch64 x86_64 x86_64_v3)
url="https://github.com/cachyos/kernel-manager"
license=(GPLv3)
depends=('qt5-base' 'polkit-qt5')
makedepends=('cmake' 'ninja' 'git')
groups=('cachyos')
source=("kernel-manager-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('3de03c0e1fa14a481e559b2be5d10c9673fe35b971750d96d548ad3ae3136a89')
provides=('cachyos-kernel-manager')
conflicts=('cachyos-kernel-manager')
options=(strip)

build() {
  cd ${srcdir}/kernel-manager-${pkgver}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build --parallel $_cpuCount
}

package() {
  cd ${srcdir}/kernel-manager-${pkgver}
  DESTDIR="${pkgdir}" cmake --build build --target install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
